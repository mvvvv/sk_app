# Renderer Example (requires Vulkan and sk_renderer)

include(FetchContent)

###############################################################################
# sk_renderer dependency (must come first to get skshaderc)
###############################################################################

message(STATUS "Fetching sk_renderer...")

set(SKR_BUILD_EXAMPLES  OFF CACHE BOOL "Disable sk_renderer examples" FORCE)
set(SKR_BUILD_SKSHADERC ON  CACHE BOOL "Enable shader compilation" FORCE)
FetchContent_Declare(
	sk_renderer
	GIT_REPOSITORY https://github.com/StereoKit/sk_renderer.git
	GIT_TAG        main
	#GIT_SHALLOW    TRUE
)
FetchContent_MakeAvailable(sk_renderer)

###############################################################################
# App definition
###############################################################################

add_skapp(renderer_example
	PACKAGE_NAME net.stereokit.renderer_example
	APP_NAME "Renderer Example"
)
target_skapp_assets       (renderer_example ${CMAKE_CURRENT_SOURCE_DIR}/assets)
target_sources            (renderer_example PRIVATE renderer_example.c)
target_link_libraries     (renderer_example PRIVATE sk_renderer)
target_include_directories(renderer_example PRIVATE
	${sk_renderer_SOURCE_DIR}/example/tools
)

if(UNIX AND NOT APPLE)
	target_link_libraries(renderer_example PRIVATE m)
endif()
if(MINGW)
	target_link_options(renderer_example PRIVATE -static-libgcc -static)
endif()

###############################################################################
# Assets and shader compilation
###############################################################################

# Compile shaders to intermediate location (mirrors final structure)
skshaderc_compile_assets(renderer_example ${CMAKE_CURRENT_BINARY_DIR}/generated/shaders "" _
	assets/shaders/default.hlsl
)

# Add source assets and compiled shaders to the app
target_skapp_assets(renderer_example ${CMAKE_CURRENT_BINARY_DIR}/generated)

message(STATUS "Building sk_renderer example")
